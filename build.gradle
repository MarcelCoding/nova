buildscript {
  ext {
    springBootVersion = '2.6.7'
    freefairVersion = '6.4.3.1'
  }
}

plugins {
  id 'base'
  id 'io.freefair.lombok' version "${freefairVersion}" // apply, because javadoc
  id 'nebula.project' version '9.6.3' apply false
  id 'com.palantir.git-version' version '0.15.0'
  id 'com.github.spotbugs' version '5.0.11' apply false
  id 'io.freefair.maven-optional' version "${freefairVersion}"
  id 'com.github.jk1.dependency-license-report' version '2.1'
  id 'io.spring.dependency-management' version '1.0.13.RELEASE' apply false

  id 'io.spring.nohttp' version '0.0.10'
  id 'io.freefair.javadoc-links' version "${freefairVersion}"
}

def gitVer = gitVersion()

version gitVer.startsWith('v')
  ? gitVer.substring(1, gitVer.endsWith('.dirty') ? gitVer.length() - 6 : gitVer.length())
  : 'edge'

allprojects {
  group 'de.m4rc3l.nova'

  repositories {
    mavenCentral()
  }
}

subprojects {
  version rootProject.version

  apply plugin: 'nebula.project'
  apply plugin: 'io.freefair.maven-optional'
  apply from: "${rootDir}/gradle/java.gradle"
  apply from: "${rootDir}/gradle/lombok.gradle"
  apply from: "${rootDir}/gradle/test.gradle"
  apply from: "${rootDir}/gradle/checkstyle.gradle"
  apply from: "${rootDir}/gradle/spotbugs.gradle"
  apply from: "${rootDir}/gradle/pmd.gradle"
  apply from: "${rootDir}/gradle/javadoc.gradle"
  apply from: "${rootDir}/gradle/publish.gradle"
}
